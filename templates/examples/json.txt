json -> value .

value -> object .
value -> array .
value -> string .
value -> number .
value -> "true" .
value -> "false" .
value -> "null" .

object -> "{" "}" .
object -> "{" members "}" .
object -> "{" premembers error "}" .

members -> member .
members -> premembers member .
premembers -> member "," .
premembers -> premembers member "," .
premembers -> error "," .
premembers -> premembers error "," .

member -> string ":" value .
member -> error ":" value .

array -> "[" "]" .
array -> "[" values "]" .
array -> "[" prevalues error "]" .

values -> value .
values -> prevalues value .
prevalues -> value "," .
prevalues -> prevalues value "," .
prevalues -> error "," .
prevalues -> prevalues error "," .
